// Client negotiates AccECN and starts sending
// AccECN option in last ACK and data segments
// while the first ACK from server is ACE=0

`../../common/defaults.sh
sysctl -q net.ipv4.tcp_ecn=3
sysctl -q net.ipv4.tcp_ecn_option=2
`

+0 socket(..., SOCK_STREAM, IPPROTO_TCP) = 4
+.002 ... 0.052 connect(4, ..., ...) = 0

+.002 > [noecn] SEWA 0:0(0) <mss 1460,sackOK,TS val 100 ecr 0,nop,wscale 8>
+0.05 < [ect0] SW. 0:0(0) ack 1 win 32767 <mss 1024,ECN e0b 1 ceb 0 e1b 1,nop,nop,nop,sackOK,nop,wscale 8>
+.002 > [ect0] A. 1:1(0) ack 1 <ECN e1b 1 ceb 0 e0b 1,nop>

// Send
+0.01 write(4, ..., 1000) = 1000
+.002 > [ect0] P.5 1:1001(1000) ack 1 <ECN e1b 1 ceb 0 e0b 1,nop>
+0.05 < [ect0] . 1:1(0) ack 1001 win 264 <ECN e1b 1 ceb 0 e0b 1001,nop>

// Send new data, it should contain AccECN option
+0.01 write(4, ..., 1000) = 1000
+.002 > [noecn] P.5 1001:2001(1000) ack 1 <ECN e1b 1 ceb 0 e0b 1,nop>

